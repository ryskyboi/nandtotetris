// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */
class Memory {
    static int freeList, largeVal;
    static Array ram, heap;

    /** Initializes the class. */
    function void init() {
        let ram = 0;
        let heap = 2048; //heapBase
        let heap[0] = 14335;
        let heap[1] = -1;
        let freeList = 2048;
        let largeVal = 14335 + 1; // Just greater than the actual largest possible
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var int prev, curr, segSize, minSize, minCurr, minPrev;
        let prev = -1;
        let minSize = largeVal;
        let curr = (freeList - heap);
        while (true) {
            let segSize = heap[curr]; //size
            if ((segSize > (size + 1)) & (segSize < minSize)) { // Just need +1 extra space with strict inequality
                let minSize = segSize;
                let minCurr = curr;
                let minPrev = prev;
            }
            if (heap[curr + 1] = -1) {// This is when you return and break up the linked list
                if (minSize = largeVal) {
                    return -1; //This is if we fail to find a suitable space
                } else {
                    if (minSize < (size + 3)) { // minSize <= size + 2
                        if (freeList = (heap + minCurr)) {
                            let freeList = heap[minCurr + 1] + heap;
                        }
                        if (~(minPrev = -1)) {
                            let heap[minPrev + 1] = heap[minCurr + 1]; //if the segment is exactly correct just set the
                        }
                        let heap[minCurr] = size + 1; //Remember the block size for de-allocation
                        return heap + (minCurr + 1); //
                    } else {
                        let heap[minCurr] = heap[minCurr] - (size + 1); // Take the value away from it plus where we store the size
                        let heap[minCurr + heap[minCurr]] = size + 1;
                        return (heap + (minCurr + heap[minCurr] + 1));
                    }
                }
            }
        let prev = curr;
        let curr = heap[curr + 1];
        }
        return -1;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        let heap[o - heap] = freeList - heap;
        let freeList = o - 1; // Point to the head where the size is
        return;
    }
}
